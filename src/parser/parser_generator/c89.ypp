/* https://www.quut.com/c/ANSI-C-grammar-l-1995.html */

%{

#include <cstdio>
#include <iostream>
#include <string>
#include "semantics/SemanticsFactory.hpp"
#include "logger/Logger.hpp"

extern char yytext[];

extern "C" int yylex( void );
extern "C" int yyparse( void );

extern int lex_current_char;
extern int lex_keyword_start_index;

void yyerror(const char *str)
{
    extern int yylineno;
    extern int column;

    fflush(stdout);
    printf("\n\nPARSING ERROR: %s (line: %d, column: %d, char_no: %d)\n\n", str, yylineno, column, lex_current_char);
    fflush(stdout);
}

auto &sem = safec::SemanticsFactory::get();

[[maybe_unused]] static void pr(
    const std::string& str,
    safec::Color color = safec::Color::Yellow)
{
    safec::log("@ % at % @", { color, safec::logger::NewLine::No })
        .arg(str)
        .arg(lex_current_char);
}

[[maybe_unused]] static void handle(
    [[maybe_unused]] const SyntaxChunkType type,
    [[maybe_unused]] const std::string &additional = "")
{
    sem.handle(type, lex_current_char, additional);
}

%}

%token IDENTIFIER CONSTANT STRING_LITERAL SIZEOF
%token PTR_OP INC_OP DEC_OP LEFT_OP RIGHT_OP LE_OP GE_OP EQ_OP NE_OP
%token AND_OP OR_OP MUL_ASSIGN DIV_ASSIGN MOD_ASSIGN ADD_ASSIGN
%token SUB_ASSIGN LEFT_ASSIGN RIGHT_ASSIGN AND_ASSIGN
%token XOR_ASSIGN OR_ASSIGN TYPE_NAME

%token TYPEDEF EXTERN STATIC AUTO REGISTER
%token CHAR SHORT INT LONG SIGNED UNSIGNED FLOAT DOUBLE CONST VOLATILE VOID
%token STRUCT UNION ENUM ELLIPSIS

%token CASE DEFAULT IF ELSE SWITCH WHILE DO FOR GOTO CONTINUE BREAK RETURN

%token SAFEC_DEFER

%union {
    char *tokenStrValue;
    int tokenIntValue;
}

%type<tokenStrValue> IDENTIFIER
%type<tokenStrValue> CONSTANT
%type<tokenStrValue> STRING_LITERAL

%start translation_unit
%%

primary_expression
    : IDENTIFIER
    {
        pr("identifier");
        handle(SyntaxChunkType::kIdentifier, $1);
        free($1);
    }
    | CONSTANT
    {
        pr("constant");
        handle(SyntaxChunkType::kConstant, $1);
        free($1);
    }
    | STRING_LITERAL
    {
        pr("string literal");
        handle(SyntaxChunkType::kConstant, $1);
        free($1);
    }
    | '(' expression ')'
    ;

postfix_expression
    : primary_expression
    | postfix_expression '[' expression ']'
    | postfix_expression '(' ')'
    {
        pr("postfix()");
        handle(SyntaxChunkType::kPostfixExpression, "()");
    }
    | postfix_expression '(' argument_expression_list ')'
    {
        pr("postfix(...)");
        handle(SyntaxChunkType::kPostfixExpression, "(...)");
    }
    | postfix_expression '.' IDENTIFIER
    | postfix_expression PTR_OP IDENTIFIER
    | postfix_expression INC_OP
    {
        pr("postfix++");
        handle(SyntaxChunkType::kPostfixExpression, "++");
    }
    | postfix_expression DEC_OP
    ;

argument_expression_list
    : assignment_expression
    | argument_expression_list ',' assignment_expression
    ;

unary_expression
    : postfix_expression
    | INC_OP unary_expression
    | DEC_OP unary_expression
    | unary_operator cast_expression
    | SIZEOF unary_expression
    | SIZEOF '(' type_name ')'
    | SAFEC_DEFER expression
    ;

unary_operator
    : '&'
    {
        pr("unary &");
        // TODO: not handled yet
    }
    | '*'
    {
        pr("unary *");
        // TODO: not handled yet
    }
    | '+'
    | '-'
    | '~'
    | '!'
    ;

cast_expression
    : unary_expression
    | '(' type_name ')' cast_expression
    ;

multiplicative_expression
    : cast_expression
    | multiplicative_expression '*' cast_expression
    | multiplicative_expression '/' cast_expression
    | multiplicative_expression '%' cast_expression
    ;

additive_expression
    : multiplicative_expression
    | additive_expression '+' multiplicative_expression
    | additive_expression '-' multiplicative_expression
    ;

shift_expression
    : additive_expression
    | shift_expression LEFT_OP additive_expression
    | shift_expression RIGHT_OP additive_expression
    ;

relational_expression
    : shift_expression
    | relational_expression '<' shift_expression
    {
        pr("relational <");
        handle(SyntaxChunkType::kRelationalExpression, "<");
    }
    | relational_expression '>' shift_expression
    | relational_expression LE_OP shift_expression
    | relational_expression GE_OP shift_expression
    ;

equality_expression
    : relational_expression
    | equality_expression EQ_OP relational_expression
    {
        handle(SyntaxChunkType::kRelationalExpression, "==");
    }
    | equality_expression NE_OP relational_expression
    {
        handle(SyntaxChunkType::kRelationalExpression, "!=");
    }
    ;

and_expression
    : equality_expression
    | and_expression '&' equality_expression
    ;

exclusive_or_expression
    : and_expression
    | exclusive_or_expression '^' and_expression
    ;

inclusive_or_expression
    : exclusive_or_expression
    | inclusive_or_expression '|' exclusive_or_expression
    ;

logical_and_expression
    : inclusive_or_expression
    | logical_and_expression AND_OP inclusive_or_expression
    ;

logical_or_expression
    : logical_and_expression
    | logical_or_expression OR_OP logical_and_expression
    ;

conditional_expression
    : logical_or_expression
    | logical_or_expression '?' expression ':' conditional_expression
    ;

assignment_expression
    : conditional_expression
    | unary_expression assignment_operator assignment_expression
    {
        pr("asgn expr");
        handle(SyntaxChunkType::kAssignment);
    }
    ;

assignment_operator
    : '='           { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "="); }
    | MUL_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "*="); }
    | DIV_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "/="); }
    | MOD_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "%="); }
    | ADD_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "+="); }
    | SUB_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "-="); }
    | LEFT_ASSIGN   { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "<<="); }
    | RIGHT_ASSIGN  { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, ">>="); }
    | AND_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "&="); }
    | XOR_ASSIGN    { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "^="); }
    | OR_ASSIGN     { pr("asgn op"); handle(SyntaxChunkType::kAssignmentOperator, "|="); }
    ;

expression
    : assignment_expression
    | expression ',' assignment_expression
    ;

constant_expression
    : conditional_expression
    ;

declaration
    : declaration_specifiers ';'
    | declaration_specifiers init_declarator_list ';'
    ;

declaration_specifiers
    : storage_class_specifier
    | storage_class_specifier declaration_specifiers
    | type_specifier
    | type_specifier declaration_specifiers
    | type_qualifier
    | type_qualifier declaration_specifiers
    ;

init_declarator_list
    : init_declarator
    | init_declarator_list ',' init_declarator
    ;

init_declarator
    : declarator
    {
        pr("init decl (no asgn)");
        handle(SyntaxChunkType::kInitDeclaration, "noasgn");
    }
    | declarator '=' { pr("asgn"); handle(SyntaxChunkType::kAssignmentOperator, "="); } initializer
    {
        pr("init decl (with asgn)");
        handle(SyntaxChunkType::kInitDeclaration, "asgn");
    }
    ;

storage_class_specifier
    : TYPEDEF
    | EXTERN
    | STATIC
    | AUTO
    | REGISTER
    ;

type_specifier
    : VOID
    { pr("type"); handle(SyntaxChunkType::kType, "void"); }
    | CHAR
    { pr("type"); handle(SyntaxChunkType::kType, "char"); }
    | SHORT
    { pr("type"); handle(SyntaxChunkType::kType, "short"); }
    | INT
    { pr("type"); handle(SyntaxChunkType::kType, "int"); }
    | LONG
    { pr("type"); handle(SyntaxChunkType::kType, "long"); }
    | FLOAT
    { pr("type"); handle(SyntaxChunkType::kType, "float"); }
    | DOUBLE
    { pr("type"); handle(SyntaxChunkType::kType, "double"); }
    | SIGNED
    { pr("type"); handle(SyntaxChunkType::kType, "signed"); }
    | UNSIGNED
    { pr("type"); handle(SyntaxChunkType::kType, "unsigned"); }
    | struct_or_union_specifier
    { pr("type"); handle(SyntaxChunkType::kType, "struct/union"); }
    | enum_specifier
    { pr("type"); handle(SyntaxChunkType::kType, "enum"); }
    | TYPE_NAME
    { pr("type"); handle(SyntaxChunkType::kType, "typename?"); }
    ;

struct_or_union_specifier
    : struct_or_union IDENTIFIER '{' struct_declaration_list '}'
    {
        pr("struct or union");
        handle(SyntaxChunkType::kStructOrUnionDecl);
    }
    | struct_or_union '{' struct_declaration_list '}'
    | struct_or_union IDENTIFIER
    ;

struct_or_union
    : STRUCT
    {
        pr("struct");
    }
    | UNION
    {
        pr("union");
    }
    ;

struct_declaration_list
    : struct_declaration
    | struct_declaration_list struct_declaration
    ;

struct_declaration
    : specifier_qualifier_list struct_declarator_list ';'
    ;

specifier_qualifier_list
    : type_specifier specifier_qualifier_list
    | type_specifier
    | type_qualifier specifier_qualifier_list
    | type_qualifier
    ;

struct_declarator_list
    : struct_declarator
    | struct_declarator_list ',' struct_declarator
    ;

struct_declarator
    : declarator
    | ':' constant_expression
    | declarator ':' constant_expression
    ;

enum_specifier
    : ENUM '{' enumerator_list '}'
    | ENUM IDENTIFIER '{' enumerator_list '}'
    | ENUM IDENTIFIER
    ;

enumerator_list
    : enumerator
    | enumerator_list ',' enumerator
    ;

enumerator
    : IDENTIFIER
    | IDENTIFIER '=' constant_expression
    ;

type_qualifier
    : CONST
    | VOLATILE
    ;

declarator
    : pointer direct_declarator
    | direct_declarator
    | reference direct_declarator
    ;

direct_declarator
    : IDENTIFIER
    {
        pr("direct decl");
        handle(SyntaxChunkType::kDirectDecl, $1);
        free($1);
    }
    | '(' declarator ')'
    | direct_declarator '[' constant_expression ']'
    | direct_declarator '[' ']'
    | direct_declarator '(' parameter_type_list ')'
    | direct_declarator '(' identifier_list ')'
    | direct_declarator '(' ')'
    ;

pointer
    : '*'
    {
        pr("ptr");
        handle(SyntaxChunkType::kPointer);
    }
    | '*' type_qualifier_list
    | '*' pointer
    {
        pr("ptr ptr");
        handle(SyntaxChunkType::kPointer);
    }
    | '*' type_qualifier_list pointer
    ;

reference
        : '&'
        | '&' type_qualifier_list
        | '&' reference
        | '&' type_qualifier_list reference
        ;

type_qualifier_list
    : type_qualifier
    | type_qualifier_list type_qualifier
    ;


parameter_type_list
    : parameter_list
    | parameter_list ',' ELLIPSIS
    ;

parameter_list
    : parameter_declaration
    | parameter_list ',' parameter_declaration
    ;

parameter_declaration
    : declaration_specifiers declarator
    | declaration_specifiers abstract_declarator
    | declaration_specifiers
    ;

identifier_list
    : IDENTIFIER
    | identifier_list ',' IDENTIFIER
    ;

type_name
    : specifier_qualifier_list
    | specifier_qualifier_list abstract_declarator
    ;

abstract_declarator
    : pointer
    | direct_abstract_declarator
    | pointer direct_abstract_declarator
    ;

direct_abstract_declarator
    : '(' abstract_declarator ')'
    | '[' ']'
    | '[' constant_expression ']'
    | direct_abstract_declarator '[' ']'
    | direct_abstract_declarator '[' constant_expression ']'
    | '(' ')'
    | '(' parameter_type_list ')'
    | direct_abstract_declarator '(' ')'
    | direct_abstract_declarator '(' parameter_type_list ')'
    ;

initializer
    : assignment_expression
    | '{' initializer_list '}'
    | '{' initializer_list ',' '}'
    ;

initializer_list
    : initializer
    | initializer_list ',' initializer
    ;

statement
    : labeled_statement
    | compound_statement
    | expression_statement
    | selection_statement
    | iteration_statement
    | jump_statement
    ;

labeled_statement
    : IDENTIFIER ':' statement
    | CASE constant_expression ':' statement
    | DEFAULT ':' statement
    ;

compound_statement
    : '{' '}'
    | '{' statement_list '}'
    | '{' declaration_list '}'
    | '{' declaration_list statement_list '}'
    ;

declaration_list
    : declaration
    | declaration_list declaration
    ;

statement_list
    : statement
    | statement_list statement
    ;

expression_statement
    : ';'
    {
        pr("empty stmt");
        handle(SyntaxChunkType::kEmptyStatement);
    }
    | expression ';'
    ;

selection_statement_if
    : IF { pr("if"); handle(SyntaxChunkType::kConditionHeader); } '(' expression ')' { pr("if cond"); handle(SyntaxChunkType::kConditionExpression); } statement { pr("if scope"); handle(SyntaxChunkType::kCondition); }
    ;

selection_statement
    : selection_statement_if
    | selection_statement_if ELSE { pr("else"); } statement { pr("else scope"); handle(SyntaxChunkType::kCondition); }
    | SWITCH '(' expression ')' statement
    ;

for_keyword
    : FOR
    {
        handle(SyntaxChunkType::kForLoopHeader);
    }
    ;

iteration_statement
    : WHILE '(' expression ')' statement
    | DO statement WHILE '(' expression ')' ';'
    | for_keyword '(' expression_statement expression_statement ')' { handle(SyntaxChunkType::kForLoopConditions); } statement { handle(SyntaxChunkType::kForLoop); }
    | for_keyword '(' expression_statement expression_statement expression ')' { handle(SyntaxChunkType::kForLoopConditions); } statement { handle(SyntaxChunkType::kForLoop); }
    ;

jump_statement
    : GOTO IDENTIFIER ';'
    | CONTINUE ';'
    | BREAK ';'
    | RETURN ';'
    | RETURN expression ';'
    {
        pr("return");
        handle(SyntaxChunkType::kReturn);
    }
    ;

translation_unit
    : external_declaration
    | translation_unit external_declaration
    ;

external_declaration
    : function_definition
    | declaration
    ;

function_definition
    : declaration_specifiers declarator declaration_list compound_statement
    | VOID declarator { pr("fun header (void)"); handle(SyntaxChunkType::kFunctionHeader, "void"); } compound_statement { pr("fun body"); handle(SyntaxChunkType::kFunction); }
    | declaration_specifiers declarator { pr("fun header (nonvoid)"); handle(SyntaxChunkType::kFunctionHeader, "nonvoid"); } compound_statement { pr("fun body"); handle(SyntaxChunkType::kFunction); }
    | declarator declaration_list compound_statement
    | declarator compound_statement
    ;

%%
